developer=http://webtrees.net/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_major=1.7
pkgver=${_major}.9
pkgrel=2
pkgdesc="Web based family history software"
arch=('any')
license=("GPL3")
source=("https://launchpad.net/webtrees/${_major}/${pkgver}/+download/webtrees-${pkgver}.zip")
releasepage=('https://launchpad.net/webtrees')
options=('!strip')
sha512sums=('eff80525080409b0fe4100b1422530a59079e5a1c8e23c5b688416f45b5016869a67087c9a8f5c3379113ea103f57dd32f8614ea4dad83872ea206604dd5ace5')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Data
    mkdir -p ${pkgdir}/var/lib/${pkgname}
    
# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/populate-db.pl ${pkgdir}/usr/share/${pkgname}/bin/

# SQL
    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    install -m644 ${startdir}/sql/tables.sql ${pkgdir}/usr/share/${pkgname}/sql/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/{config.ini.php,htaccess}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# The app
    cp -dr --no-preserve=ownership ${startdir}/src/webtrees ${pkgdir}/usr/share/${pkgname}/
}


