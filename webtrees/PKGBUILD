developer=http://webtrees.net/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_major=2.1
pkgver=${_major}.18
pkgrel=1
pkgdesc="Online genealogy"
arch=('any')
license=("GPL3")
source=("https://github.com/fisharebest/webtrees/releases/download/${pkgver}/webtrees-${pkgver}.zip")
releasepage=('https://github.com/fisharebest/webtrees/releases')
options=('!strip')
sha512sums=('93842a81c29d834a7a93b1808682a0666c701ae9a37e9429154260c68bc68657ee98297aa32709d97cff304af653b86ce1d1446c1783a24329a796242718f11b')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# SQL
    install -D -m644 ${startdir}/sql/*.sql* -t ${pkgdir}/ubos/share/${pkgname}/sql/

# Templates
    install -D -m644 ${startdir}/tmpl/{config.ini.php,htaccess}.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# The app
    cp -dr --no-preserve=ownership ${startdir}/src/webtrees ${pkgdir}/ubos/share/${pkgname}/
}
