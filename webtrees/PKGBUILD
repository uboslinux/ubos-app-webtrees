developer=http://webtrees.net/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_major=1.7
pkgver=${_major}.16
pkgrel=1
pkgdesc="Web based family history software"
arch=('any')
license=("GPL3")
source=("https://github.com/fisharebest/webtrees/releases/download/${pkgver}/webtrees-${pkgver}.zip")
releasepage=('https://github.com/fisharebest/webtrees/releases')
options=('!strip')
sha512sums=('31e4800138176fa6a7c76db8cd042740cbf158f4b7deb7728e3dde94055a5b37be18a48dcb9dc935224f066026e48c5ab2fca8635f26b9a595c71ad098ce7d8a')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/populate-db.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# SQL
    install -D -m644 ${startdir}/sql/tables.sql -t ${pkgdir}/ubos/share/${pkgname}/sql/

# Templates
    install -D -m644 ${startdir}/tmpl/{config.ini.php,htaccess}.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# The app
    cp -dr --no-preserve=ownership ${startdir}/src/webtrees ${pkgdir}/ubos/share/${pkgname}/
}
